;****************************************************************************************
;**		Less Pushover Merchants 0.2
;**		Based on No Pushover Merchants 2.0 by ComradeSokami
;**		Created by otikscypi
;****************************************************************************************

; Version 0.1 - PoC, only single trainer
; Version 0.2 - PoC, code refactor
; Version 1.0 - 3x ESP: 1. only traders, who are not trainers, with max skill 2. only traders, who are not trainers, with safe skill, 3. trainers
; Version 2.0 - everyone
;
; For an actor check all his/hers skills. Select top three skills and their values.
; Check also the Personality of the actor.
;; If there is Mercantile among top three skills - do not change it value
;; If there is no Mercantile, check if Personality is larger then lowest skill 
;;; If it is take the value of the lowest of the top skills, subtract 1 and set Mercantile to this value
;;; If it is not take the value of the Personality and set Mercantile to this value
;; Repeat for Speechcraft
; Repeat for every actor subject to rebalance
;
; Index of top three skills
;
; 0 - Block
; 1 - Armorer
; 2 - MediumArmor
; 3 - HeavyArmor
; 4 - BluntWeapon
; 5 - LongBlade
; 6 - Axe
; 7 - Spear
; 8 - Athletics
; 9 - Enchant
; 10 - Destruction
; 11 - Alteration
; 12 - Illusion
; 13 - Conjuration
; 14 - Mysticism
; 15 - Restoration
; 16 - Alchemy
; 17 - Unarmored
; 18 - Security
; 19 - Sneak
; 20 - Acrobatics
; 21 - LightArmor
; 22 - ShortBlade
; 23 - Marksman
; 24 - Mercantile
; 25 - Speechcraft
; 26 - HandToHand


begin NoPushoverMerchants

	short doOnce ; 1

	short skillIndex ; 2
	short tempSkillValue ; 3

	short topSkill ; 4
	short topSkillValue ; 5
	short secondSkill ; 6
	short secondSkillValue ; 7
	short thirdSkill ; 8
	short thirdSkillValue ; 9

	short isMercantileTrainer ; 10 ; faux boolean 1 - true 0 - false
	short isSpeechcraftTrainer ; 11 ; faux boolean 1 - true 0 - false 

	short actorsPersonality ; 12

	short finalSkillValue ; 13

	if ( doOnce == 0 )
		MessageBox, "Less Pushover Merchants Running"

		; flaenia amiulusus

		; get top character's skills

		while (skillIndex <= 26)

			if (skillIndex == 0)
				set tempSkillValue to ("flaenia amiulusus"->GetBlock)			
			elseif (skillIndex == 1)
				set tempSkillValue to ("flaenia amiulusus"->GetArmorer)
			elseif (skillIndex == 2)
				set tempSkillValue to ("flaenia amiulusus"->GetMediumArmor)
			elseif (skillIndex == 3)
				set tempSkillValue to ("flaenia amiulusus"->GetHeavyArmor)
			elseif (skillIndex == 4)
				set tempSkillValue to ("flaenia amiulusus"->GetBluntWeapon)
			elseif (skillIndex == 5)
				set tempSkillValue to ("flaenia amiulusus"->GetLongBlade)
			elseif (skillIndex == 6)
				set tempSkillValue to ("flaenia amiulusus"->GetAxe)
			elseif (skillIndex == 7)
				set tempSkillValue to ("flaenia amiulusus"->GetSpear)
			elseif (skillIndex == 8)
				set tempSkillValue to ("flaenia amiulusus"->GetAthletics)
			elseif (skillIndex == 9)
				set tempSkillValue to ("flaenia amiulusus"->GetEnchant)
			elseif (skillIndex == 10)
				set tempSkillValue to ("flaenia amiulusus"->GetDestruction)
			elseif (skillIndex == 11)
				set tempSkillValue to ("flaenia amiulusus"->GetAlteration)
			elseif (skillIndex == 12)
				set tempSkillValue to ("flaenia amiulusus"->GetIllusion)
			elseif (skillIndex == 13)
				set tempSkillValue to ("flaenia amiulusus"->GetConjuration)
			elseif (skillIndex == 14)
				set tempSkillValue to ("flaenia amiulusus"->GetMysticism)
			elseif (skillIndex == 15)
				set tempSkillValue to ("flaenia amiulusus"->GetRestoration)
			elseif (skillIndex == 16)
				set tempSkillValue to ("flaenia amiulusus"->GetAlchemy)
			elseif (skillIndex == 17)
				set tempSkillValue to ("flaenia amiulusus"->GetUnarmored)
			elseif (skillIndex == 18)
				set tempSkillValue to ("flaenia amiulusus"->GetSecurity)
			elseif (skillIndex == 19)
				set tempSkillValue to ("flaenia amiulusus"->GetSneak)
			elseif (skillIndex == 20)
				set tempSkillValue to ("flaenia amiulusus"->GetAcrobatics)
			elseif (skillIndex == 21)
				set tempSkillValue to ("flaenia amiulusus"->GetLightArmor)
			elseif (skillIndex == 22)
				set tempSkillValue to ("flaenia amiulusus"->GetShortBlade)
			elseif (skillIndex == 23)
				set tempSkillValue to ("flaenia amiulusus"->GetMarksman)
			elseif (skillIndex == 24)
				set tempSkillValue to ("flaenia amiulusus"->GetMercantile)
			elseif (skillIndex == 25)
				set tempSkillValue to ("flaenia amiulusus"->GetSpeechcraft)
			elseif (skillIndex == 26)
				set tempSkillValue to ("flaenia amiulusus"->GetHandToHand)
			else
				MessageBox, "Less Pushover Merchants - error during establishing top three skills"
			endif

			if  (tempSkillValue > topSkillValue)
				set thirdSkillValue to secondSkillValue
				set thirdSkill to secondSkill
				set secondSkillValue to topSkillValue
				set secondSkill to topSkill
				set topSkillValue to tempSkillValue
				set topSkill to skillIndex
			elseif (tempSkillValue > secondSkillValue)
				set thirdSkillValue to secondSkillValue
				set thirdSkill to secondSkill
				set secondSkillValue to tempSkillValue
				set secondSkill to skillIndex
			elseif (tempSkillValue > thirdSkillValue)
				set thirdSkillValue to tempSkillValue
				set thirdSkill to skillIndex
			endif

			set skillIndex to (skillIndex + 1)
			
		endwhile

		; check if actor is a potential mercantile trainer

		set isMercantileTrainer to 1

		if (topSkill != 24)
			if (secondSkill != 24)
				if (thirdSkill != 24)
					set isMercantileTrainer to 0
				endif
			endif
		endif

		; check if actor is a potential speechcraft trainer

		set isSpeechcraftTrainer to 1

		if (topSkill != 25)
			if (secondSkill != 25)
				if (thirdSkill != 25)
					set isSpeechcraftTrainer to 0
				endif
			endif
		endif

		; get actor's personality

		set actorsPersonality to ( "flaenia amiulusus"->GetPersonality )

		; update mercantile

		if (isMercantileTrainer == 0)
			if (actorsPersonality >= thirdSkillValue)
				set finalSkillValue to (thirdSkillValue - 1) 
			else
				set finalSkillValue to actorsPersonality
			endif
			"flaenia amiulusus"-> SetMercantile, finalSkillValue
		endif

		; update speechcraft

		if (isSpeechcraftTrainer == 0)
			if (actorsPersonality >= thirdSkillValue)
				set finalSkillValue to (thirdSkillValue - 1) 
			else
				set finalSkillValue to actorsPersonality
			endif
			"flaenia amiulusus"-> SetSpeechcraft, finalSkillValue
		endif		

		MessageBox, "Merchants' and trainers' stats updated"
		stopscript, NoPushoverMerchants

		Set doOnce to -1
	endif

End